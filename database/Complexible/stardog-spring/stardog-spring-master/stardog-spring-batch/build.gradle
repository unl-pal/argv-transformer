apply plugin:"java"
apply plugin:"eclipse"
apply plugin:"maven"
apply plugin:"signing"

//apply from:'https://raw.github.com/breskeby/gradleplugins/master/emmaPlugin/emma.gradle'

group = 'com.stardog.ext'
version = '1.1.0'

ext {
	stardogVersion = "7.9.1"
	stardogSpringVersion = "1.1.0"
	springVersion = "5.3.18"
	springBatchVersion = "4.3.5"
	junitVersion = "4.13.2"
	slf4jVersion = "1.6.1"
	log4jVersion = "2.17.0"
}

repositories {
	maven { url "http://maven.stardog.com" }
	mavenLocal()
	mavenCentral()
	
	maven { url "http://repository.springsource.com/maven/bundles/release" }
	maven { url "http://repository.springsource.com/maven/bundles/external" } 
	maven { url "http://maven.springframework.org/release" } 
	maven { url "http://maven.springframework.org/milestone" }
	maven { url "http://maven.springframework.org/snapshot" } 
	
}

configurations { 
	//emma
}

dependencies {
	// Core Dependencies
	compile ("org.springframework:spring-core:${springVersion}")
	compile ("org.springframework:spring-beans:${springVersion}")
	compile ("org.springframework:spring-context:${springVersion}")
	compile ("org.springframework:spring-tx:${springVersion}")
	compile ("org.slf4j:slf4j-api:${slf4jVersion}")

	compile ("org.springframework.batch:spring-batch-core:${springBatchVersion}")
	compile ("org.springframework.batch:spring-batch-infrastructure:${springBatchVersion}")

	compile ("com.complexible.stardog:client-http:${stardogVersion}")
	compile ("com.stardog.ext:stardog-spring:${stardogSpringVersion}")
	testCompile ("com.complexible.stardog:server:${stardogVersion}") {
		 exclude group: 'com.complexible.stardog.virtual'
	 }	
	
	testCompile group: 'junit', name: 'junit', version: "${junitVersion}"
	testCompile group: 'org.springframework', name:'spring-test', version:"${springVersion}"

	constraints {
		implementation ("org.apache.logging.log4j:log4j-core:${log4jVersion}") {
			because "'Log4Shell' vulnerability"
		}
		implementation ("org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}") {
			because "'Log4Shell' vulnerability"
		}
	}
	
	//emma "emma:emma:2.0.5312"
	//emma "emma:emma_ant:2.0.5312"
	
}
/*
emma {
	reportPath = "reports"
}
*/

tasks.withType(Test) {
	println "**********************"
	println "As of Stardog 7.0.0, for the tests to work you need to set the environment variable"
	println "STARDOG_LIB to the lib directory of your Stardog 7.X installation directory"
	println "Your current STARDOG_LIB is " + System.getenv( 'STARDOG_LIB' )
	println "**********************"
    systemProperty "java.library.path", System.env['STARDOG_LIB']
}

task sourceJar(type: Jar) {
	description 'An archive of the source code for Maven Central'
	classifier 'sources'
	from sourceSets.main.java
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}


artifacts {
	archives javadocJar, sourceJar
}

signing {
	sign configurations.archives
	required { gradle.taskGraph.hasTask(uploadArchives) }
}

 uploadArchives {
 	repositories {
 		mavenDeployer {
 			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

 			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
 				authentication(userName: sonatypeUsername, password: sonatypePassword)
 			}

 			pom.project {
 				name 'stardog-spring-batch'
 				packaging 'jar'
 				description 'Stardog Spring bindings for using Stardog with the Springframework Batch subproject'
 				url 'https://github.com/stardog-union/stardog-spring/stardog-spring-batch'

 				scm {
					url 'scm:git://github.com/stardog-union/stardog-spring.git'
					connection 'scm:git://github.com/stardog-union/stardog-spring.git'
					developerConnection 'scm:git://github.com/stardog-union/stardog-spring.git'
 				}

 				licenses {
 					license {
 						name 'The Apache Software License, Version 2.0'
 						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
 						distribution 'repo'
 					}
 				}

 				developers {
 					developer {
 						id 'albakercp'
 						name 'Al Baker'
 					}
 					developer {
 						id 'gcoluni'
 						name 'Gregory Coluni'
 					}
					developer {
						id 'stardog_support'
						name 'Stardog Support'
					}
 				}
 			}
 		}
 	}
 }

 configure(install.repositories.mavenInstaller) {
 	pom.project {
 		version '1.1.0'
 		artifactId 'stardog-spring-batch'
 		groupId 'com.stardog.ext'
 	}
 }

wrapper {
	gradleVersion = '6.0.1'
}
